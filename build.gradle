buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
    }
}

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'jacoco'
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    mavenCentral()
    jcenter()
}

mainClassName = "org.broadinstitute.hail.driver.Main"

String scalaVersion = '2.10.6'
String scalaMajorVersion = '2.10'
// String scalaVersion = '2.11.7'
// String scalaMajorVersion = '2.11'

compileScala {
    scalaCompileOptions.additionalParameters = ["-feature"]
    // scalaCompileOptions.forkOptions.jvmArgs = ['-XX:MaxPermSize=512m']
}

shadowJar {
    manifest {
        attributes 'Implementation-Title': 'hail',
                'Implementation-Version': '0.0.1-SNAPSHOT',
                'Main-Class': 'org.broadinstitute.hail.driver.Main'
    }
    baseName = project.name + '-all'
    classifier = 'spark'
    mergeServiceFiles()
    zip64 true
    // conflict with version of math3 in default Hadoop/Spark install
    relocate 'org.apache.commons', 'org.broadinstitute.hail.relocated.org.apache.commons'
    dependencies {
        include(dependency('net.jpountz.lz4:lz4:.*'))
        include(dependency('org.apache.commons:commons-math3:.*'))
        include(dependency('org.scalanlp:breeze_' + scalaMajorVersion + ':.*'))
        include(dependency('args4j:args4j:.*'))
        include(dependency('com.github.samtools:htsjdk:.*'))

        // for GC
        // include(dependency('org.scala-lang:scala-compiler:.*'))
        // include(dependency('org.scala-lang:scala-library:.*'))
        // include(dependency('org.scala-lang:scala-reflect:.*'))
    }

}

dependencies {
    compile 'org.scala-lang:scala-compiler:' + scalaVersion
    compile 'org.scala-lang:scala-library:' + scalaVersion
    compile 'org.scala-lang:scala-reflect:' + scalaVersion
    // compile 'org.apache.hadoop:hadoop-aws:2.7.1'
    compile('org.apache.spark:spark-core_' + scalaMajorVersion + ':1.4.1') {
        exclude module: 'hadoop-client'
    }
    compile ('org.apache.hadoop:hadoop-client:2.7.1') {
        exclude module: 'servlet-api'
    }
    //compile 'org.apache.spark:spark-core_' + scalaMajorVersion + ':1.4.1'
    compile 'org.apache.spark:spark-sql_' + scalaMajorVersion + ':1.4.1'
    compile 'org.apache.spark:spark-mllib_' + scalaMajorVersion + ':1.4.1'
    compile 'net.jpountz.lz4:lz4:1.3.0'
    compile 'org.testng:testng:6.8.21'
    compile 'org.scalatest:scalatest_' + scalaMajorVersion + ':2.2.4'
    compile 'org.apache.commons:commons-math3:3.5'
    compile 'org.scalacheck:scalacheck_' + scalaMajorVersion + ':1.12.4'
    compile 'org.scalanlp:breeze_' + scalaMajorVersion + ':0.11.2'
    compile 'args4j:args4j:2.32'
    compile 'com.github.samtools:htsjdk:1.139'
}

test {
    useTestNG {
    }

    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/reports/coverage"
    }
}

task coverage(dependsOn: jacocoTestReport)

task testJar(type: Jar) {
    classifier = 'tests'
    from sourceSets.test.output
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

compileScala {
    scalaCompileOptions.useAnt = false
}
